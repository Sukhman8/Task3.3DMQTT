#include <WiFiNINA.h>
#include <PubSubClient.h>

// ===== WiFi credentials =====
const char* ssid = "Makkar's S23";      
const char* password = "makkar@123"; 

// ===== MQTT broker =====
const char* mqttServer = "broker.emqx.io";  
const int mqttPort = 1883;                   
const char* mqttTopicWave  = "SIT210/wave";  
const char* mqttTopicPat   = "SIT210/pat";  

// ===== Objects =====
WiFiClient wifiClient;
PubSubClient client(wifiClient);

// ===== Pins =====
const int trigPin = 2;  
const int echoPin = 3;  
const int ledPin  = 7; 

// ===== Timing control =====
unsigned long lastWave = 0; //used for time values
unsigned long lastPat = 0;
const unsigned long cooldown = 2000; 
long getDistance() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  long duration = pulseIn(echoPin, HIGH, 30000); 
  return duration * 0.034 / 2; 
}

void flashLED(int times, int delayMs) {
  for (int i = 0; i < times; i++) {
    digitalWrite(ledPin, HIGH);
    delay(delayMs);
    digitalWrite(ledPin, LOW);
    delay(delayMs);
  }
}

void callback(char* topic, byte* payload, unsigned int length) {
  String message;
  for (unsigned int i = 0; i < length; i++) {
    message += (char)payload[i];
  }

  Serial.print("Message received [");
  Serial.print(topic);
  Serial.print("]: ");
  Serial.println(message);

  // LED reaction when receiving MQTT messages
  if (String(topic) == mqttTopicWave) {
    flashLED(3, 300);   // Blink LED 3 times slowly 
  }
  else if (String(topic) == mqttTopicPat) {
    flashLED(5, 150);   // Blink LED 5 times fast 
  }
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection to ");
    Serial.print(mqttServer);
    Serial.print(":");
    Serial.println(mqttPort);

    String clientId = "Sukhman_webclient_22";
    clientId += String(random(0xffff), HEX);

    if (client.connect(clientId.c_str())) {
      Serial.println("MQTT connected!");
      client.subscribe(mqttTopicWave);
      client.subscribe(mqttTopicPat);
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" (retry in 2s)");
      delay(2000);
    }
  }
}

void setup() {
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(ledPin, OUTPUT);

  Serial.begin(115200);

  // Connect to WiFi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.print("Connected to WiFi! IP: ");
  Serial.println(WiFi.localIP());

  // Setup MQTT
  client.setServer(mqttServer, mqttPort);
  client.setCallback(callback);

  // Seed random generator
  randomSeed(analogRead(0));
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  long distance = getDistance();
  Serial.print("Distance: ");
  Serial.println(distance);

  
  if (distance >= 50 && distance <= 100 && millis() - lastWave > cooldown) {
    client.publish(mqttTopicWave, "sukhman");
    Serial.println("Published: Wave detected by Sukhman!");
    lastWave = millis();
  }

  
  if (distance > 0 && distance < 30 && millis() - lastPat > cooldown) {
    client.publish(mqttTopicPat, "Sukhman");
    Serial.println("Published: Pat detected by Sukhman!");
    lastPat = millis();
  }

  delay(200); 
}
